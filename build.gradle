// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'kotlin'
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: 'maven-publish'
apply plugin: 'signing'

buildscript {
    ext.kotlin_version = '1.7.20'
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.3.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.20"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:11.0.0"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "com.google.devtools.ksp:com.google.devtools.ksp.gradle.plugin:1.7.20-1.0.7"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
    ext.artefactVersion = "0.1"
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://maven.google.com' }
        google()
    }

    ktlint {
        // disabled because java imports put to the list end by intellij
        disabledRules = [
                "import-ordering",
                "chain-wrapping",
        ]

        // Workaround for ktlint memory leak; remove when issue got fixed
        //   https://github.com/JLLeitschuh/ktlint-gradle/issues/507
        version = "0.46.1"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.github.schwarzit'
            artifactId = 'kokain'
            version = "$artefactVersion"
            from components.java

            pom {
                name = 'Kokain DI Framework'
                description = 'Kokain a pragmatic lightweight dependency injection framework for Kotlin based Applications.'
                url = 'https://schwarzit.github.io/kokain/'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'android_development'
                        name = 'SchwarzIT'
                        email = 'android_development@mail.schwarz'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/SchwarzIT/kokain.git'
                    developerConnection = 'scm:git:ssh://github.com/SchwarzIT/kokain.git'
                    url = 'https://github.com/SchwarzIT/kokain.git'
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username = "$ossrhUsername"
                password = "$ossrhPassword"
            }
            def releaseRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = artefactVersion.endsWith('SNAPSHOT') ? snapshotRepoUrl : releaseRepoUrl
        }
    }
}

signing {
    sign configurations.archives
    sign publishing.publications.mavenJava
}
