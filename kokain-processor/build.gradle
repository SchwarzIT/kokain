apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'pmd'
apply plugin: 'kotlin-kapt'
apply plugin: "org.jlleitschuh.gradle.ktlint"

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.squareup:kotlinpoet:1.15.3'
    implementation 'com.squareup:kotlinpoet-metadata:1.15.3'
    implementation project(path: ':kokain-generator-lib', configuration: 'default')
    implementation 'org.apache.commons:commons-lang3:3.4'
    testImplementation 'junit:junit:4.12'
    testImplementation project(path: ':kokain-core-api', configuration: 'default')
    testImplementation 'dev.zacsweers.kctfork:core:0.4.0'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    kapt "com.google.auto.service:auto-service:1.0-rc7"
    compileOnly "com.google.auto.service:auto-service:1.0-rc7"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

}

targetCompatibility = JavaVersion.VERSION_17
sourceCompatibility = JavaVersion.VERSION_17

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            from components.java
        }
    }
}


jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    group 'verification'
    dependsOn 'test'

    reports {

        xml.required = true
        html.required = false
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

if (JavaVersion.current() >= JavaVersion.VERSION_16) {
    test {
        jvmArgs(
                "--add-opens=jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED",
                "--add-opens=jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED",
                "--add-opens=jdk.compiler/com.sun.tools.javac.comp=ALL-UNNAMED",
                "--add-opens=jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED",
                "--add-opens=jdk.compiler/com.sun.tools.javac.jvm=ALL-UNNAMED",
                "--add-opens=jdk.compiler/com.sun.tools.javac.main=ALL-UNNAMED",
                "--add-opens=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED",
                "--add-opens=jdk.compiler/com.sun.tools.javac.processing=ALL-UNNAMED",
                "--add-opens=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED",
                "--add-opens=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED",
        )
    }
}



